0 VALUE P#NUMS  0 VALUE P#TOTAL
6 3 * CELLS ALLOCATE THROW VALUE SOLUTION  \ solution chain: P#, value, last index in P#NUMS
: 'P  ( p -- xt )   8 * PAD + @ ;   \ vector to P# function
: P-BOR  ( p -- end beg )   8 * PAD 5 + + DUP C@ SWAP 1- C@ ;
: P-BORDERS  ( xt -- beg end )  0  
   BEGIN  1+ DUP 2 PICK EXECUTE 999 > UNTIL DUP 
   BEGIN  1+ DUP 3 PICK EXECUTE 9999 > UNTIL ROT DROP ;
: P-INIT   ( -- )   \ determine P funcs XTs & borders 
   9 3 DO 
      I 48 + PAD 2+ C!  [CHAR] P PAD 1+ C!  2 PAD C!
      PAD FIND  0= ABORT" P# function not found "
      DUP PAD I 8 * + !  P-BORDERS
      I 8 * PAD 4 + + DUP 1+ ROT 
      SWAP C! C! 
   LOOP ;
: P3  ( n -- u )   DUP 1+ * 2/ ;       \ Triangle
: P4  ( n -- u )   DUP * ;             \ Square
: P5  ( n -- u )   DUP 3 * 1- * 2/ ;   \ Pentagonal 
: P6  ( n -- u )   DUP 2 * 1- * ;      \ Hexagonal
: P7  ( n -- u )   DUP 5 * 3 - * 2/ ;  \ Heptagonal
: P8  ( n -- u )   DUP 3 * 2- * ;      \ Octagonal
: PNUMSGEN  ( -- # )   0 \ generate all 4-digits p-nums, #: total number of
   HERE TO P#NUMS 
   3 8 DO I P-BOR 
      DO I J 'P EXECUTE 
         DUP 100 MOD 9 >  \ drop leading 0
         IF J , , 1+ ELSE DROP THEN 
      LOOP  -1 +LOOP ;
: K-ZERO  ( k -- )   \ rollback k's solution
   DUP 3 * CELLS SOLUTION + 2 CELLS ERASE 
   DUP 5 < IF 1+ 3 * CELLS 2 CELLS SOLUTION + + CELL ERASE ELSE DROP THEN ;
: ROLLBACK  ( k -- k-1 )   DUP K-ZERO 1- ;
: SOL  ( k -- p u i )   \ k's solution P#, value, last i in P#NUM
   3 * CELLS SOLUTION + DUP @  OVER CELL+ @  ROT 5 CELLS + @ ;
: RESTORE  ( -- )   DUP SOL DROP NIP 100 MOD ;
: SOL-CHEK  ( -- )   \ if solved write answer to PAD
   SOLUTION CELL+ @ 100 /  SOLUTION 16 CELLS + @ 100 MOD
   = IF 0 6 0 DO I 3 * CELLS CELL+ SOLUTION + @ + LOOP PAD ! THEN ;
: UNIQ-P?  ( p -- ? )   \ check if no such P# in solution
   SOLUTION 5 0
   DO DUP @ 2 PICK = IF 2DROP UNLOOP FALSE EXIT THEN  3 CELLS +
   LOOP 2DROP TRUE ;
: UNIQ-U?  ( u -- ? )   \ check if no such value in solution
   SOLUTION CELL+ 5 0
   DO DUP @ 2 PICK = IF 2DROP UNLOOP FALSE EXIT THEN  3 CELLS +
   LOOP 2DROP TRUE ;
: P#NUM  ( i -- p u )   \ i's P#NUMS P# & value
   2* CELLS P#NUMS + DUP @ SWAP CELL+ @ ;
: P#NTOSOL  ( i k -- )   \ copy i's P#NUM to k's SOLUTION
   3 * CELLS SOLUTION + SWAP  DUP 2* CELLS P#NUMS + DUP @
   3 PICK !  CELL+ @  2 PICK CELL+ !  SWAP 2 CELLS + ! ;
: P#NEXT  ( k -- )  \ find next solution recursively, k: solution length
   DUP SOL  ROT DROP SWAP
   100 MOD  P#TOTAL ROT
   DO I P#NUM  SWAP UNIQ-P?
      IF 100 / OVER = 
         IF I P#NUM NIP UNIQ-U?
            IF DROP 1+ 
               I OVER P#NTOSOL  DUP 5 =    \ is all 6 values in solution?
               IF SOL-CHEK ROLLBACK RESTORE 
               ELSE RECURSE  RESTORE THEN
            THEN
         THEN
      ELSE DROP THEN
   LOOP DROP ROLLBACK ;   
: BRUT  ( -- u )   0 PAD !  0
   BEGIN 
      DUP CELLS P#NUMS + DUP @ SWAP CELL+ @ SWAP SOLUTION ! SOLUTION CELL+ ! 
      0 P#NEXT DROP 2+
      PAD @ ?DUP
   UNTIL NIP ;  
P-INIT PNUMSGEN TO P#TOTAL BRUT .

\EOF 28684  :  1281, 8128, 2882, 8256, 5625, 2512


Cyclical figurate numbers
Problem 61
Triangle, square, pentagonal, hexagonal, heptagonal, and octagonal numbers are all figurate (polygonal) numbers and are generated by the following formulae:
Triangle 	  	P3,n=n(n+1)/2 	  	1, 3, 6, 10, 15, ...
Square 	  	   P4,n=n**2 	   	1, 4, 9, 16, 25, ...
Pentagonal 	  	P5,n=n(3n-1)/2 	  	1, 5, 12, 22, 35, ...
Hexagonal 	  	P6,n=n(2n-1) 	  	1, 6, 15, 28, 45, ...
Heptagonal 	  	P7,n=n(5n-3)/2 	  	1, 7, 18, 34, 55, ...
Octagonal 	  	P8,n=n(3n-2) 	  	1, 8, 21, 40, 65, ...
The ordered set of three 4-digit numbers: 8128, 2882, 8281, has three interesting properties.
1. The set is cyclic, in that the last two digits of each number is the first two digits of the next number (including the last number with the first).
2. Each polygonal type: triangle (P3,127=8128), square (P4,91=8281), and pentagonal (P5,44=2882), is represented by a different number in the set.
3. This is the only set of 4-digit numbers with this property.
Find the sum of the only ordered set of six cyclic 4-digit numbers for which each polygonal type: triangle, square, pentagonal, hexagonal, heptagonal, and octagonal, is represented by a different number in the set.
